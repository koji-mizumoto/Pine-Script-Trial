// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © kaneda_cocoroto_co

//@version=6
strategy("[3]中期MA, 長期MAのGC/DC, BB/RSI30%表示", overlay=true, margin_long=100, margin_short=100)

// 価格がボリンジャーバンドの下限を割り込み、RSIが30以下の場合、反発のサイン。
// 反発の予想。買いエントリーではないので注意する。
// 期間によってシグナルの位置が変わる。ダマシもある。

// 買い：中期MA > 長期MAのゴールデンクロス
// 売り：中未MA < 長期MAのデッドクロス

// input
BB_length = input.int(defval = 20, title = "ボリンジャーバンド：期間", minval = 1)
BB_mult = input.int(defval = 2, title = "ボリンジャーバンド：標準偏差", minval = 1, maxval = 8)

// input MA
MA_middle_length = input.int(defval = 25, title = "中期MA", minval = 1)
MA_Long_length = input.int(defval = 75, title = "長期MA", minval = 1)

// input RSI Background
rsi_length = input.int(defval = 14, title = "長期RSI期間", minval = 1)
rsi_threshold = input.int(defval = 30, title = "RSIの下のしきい値", minval = 1)
rsi_up_threshold = input.int(defval = 80, title = "RSIの上のしきい値", minval = 1)

// BB calc
[BB_middle, BB_Upper, BB_Lower] = ta.bb(close, BB_length, BB_mult)

// BB plot / fill
plot(BB_middle, color = color.white, style = plot.style_circles, title = "ボリンジャーバンド")
upperBand = plot(BB_Upper, color = color.new(color.blue, 70), title = "上部バンド")
lowerBand = plot(BB_Lower, color = color.new(color.blue, 70), title = "下部バンド")
fill(upperBand, lowerBand, color = color.new(color.blue, 90), title = "BB背景")

// 上限を突き抜けているか
isAboveUpperBand = close > BB_Upper

// 下限を突き抜けているか
isBelowLowerBand = close < BB_Lower

// MAの計算
MA_middle = ta.sma(close, MA_middle_length)
MA_Long = ta.sma(close, MA_Long_length)

// RSIの計算
rsi = ta.rsi(close, rsi_length)

// 反発シグナル
BB_RSI_signal = isBelowLowerBand and rsi < rsi_threshold

// 売買シグナル
MA_GC_signal = ta.crossover(MA_middle, MA_Long)
MA_DC_signal = ta.crossunder(MA_middle, MA_Long)

// 描画
plot(MA_middle, title = "中期MA", color = color.green, linewidth = 2)
plot(MA_Long, title = "長期MA", color = color.aqua)
barcolor(isAboveUpperBand or isBelowLowerBand ? color.yellow : na, title = "BBを抜けている")
plotshape(BB_RSI_signal, title = "反発のサイン",  color = color.white, style = shape.flag, location = location.belowbar,
     text = "反発のサイン", textcolor = color.white, size = size.small)
plotshape(MA_GC_signal, title = "買いサイン", color = color.red, style = shape.triangleup, location = location.belowbar,
     text = "BUY", textcolor = color.red, size = size.small)
plotshape(MA_DC_signal, title = "売りサイン", color = color.green, style = shape.triangledown, location = location.abovebar,
     text = "SELL", textcolor = color.green, size = size.small)


longCondition = ta.crossover(MA_middle, MA_Long)
if (longCondition)
    strategy.entry("My Long Entry Id", strategy.long)

shortCondition = ta.crossunder(MA_middle, MA_Long)
if (shortCondition)
    strategy.entry("My Short Entry Id", strategy.short)
